apply plugin: 'maven-publish'

def LIB_GROUP_ID = 'kz.btsdigital.aitu'
def LIB_ARTIFACT_ID = 'embedded'
def LIB_VERSION = '0.1'

def githubUser = ""
def githubAccessToken = ""

file("../publish.properties").with { file ->
    if (file.canRead()) {
        def properties = new Properties()
        properties.load(new FileInputStream(file))
        githubUser = properties['GITHUB_USER']
        githubAccessToken = properties['GITHUB_PERSONAL_ACCESS_TOKEN']
    } else {
        println 'Unable to read publish.properties'
    }
}


task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    repositories {
        maven {
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/btsdigital/AituEmbeddedAndroid")
            credentials {
                username = System.getenv('GITHUB_USER') ?: githubUser
                password = System.getenv('GITHUB_PERSONAL_ACCESS_TOKEN') ?: githubAccessToken
            }
        }
        maven {
            name = 'LocalMavenRepo'
            url = "file://${buildDir}/repo"
        }
    }
    publications {
        aituEmbedded(MavenPublication) {
            groupId LIB_GROUP_ID
            artifactId LIB_ARTIFACT_ID
            version LIB_VERSION
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
            artifact(sourceJar)
        }
    }
}